import me.champeau.jmh.JmhBytecodeGeneratorTask
import org.ajoberstar.grgit.Grgit
import org.gradle.internal.os.OperatingSystem

import java.time.Duration

plugins {
    id 'java'
    id 'scala'
    id 'me.champeau.jmh' version '0.7.1'
    id 'org.ajoberstar.grgit' version '5.2.0'
    id 'pl.allegro.tech.build.axion-release' version '1.15.5'
    id 'io.github.gradle-nexus.publish-plugin' version '1.3.0'
    id 'maven-publish'
    id 'signing'
}

scmVersion {
    versionCreator('versionWithBranch')
    tag {
        prefix = ''
    }
}

group = 'org.simdjson'
version = scmVersion.version

repositories {
    mavenLocal()
    mavenCentral()
}

java {
    // It seems that specifying the minimum supported Java version while allowing the use of newer
    // ones isn't possible in Gradle. To test the library against multiple Java versions, the
    // workaround proposed in https://github.com/gradle/gradle/issues/16256 has been applied:
    if (!JavaVersion.current().isCompatibleWith(JavaVersion.VERSION_18)) {
        toolchain {
            languageVersion = JavaLanguageVersion.of(18)
        }
    }
    withJavadocJar()
    withSourcesJar()
}

ext {
    junitVersion = '5.10.2'
    jsoniterScalaVersion = '2.28.4'
    lombokVersion = '1.18.34'
}

dependencies {
    jmhImplementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.17.0'
    jmhImplementation group: 'com.alibaba.fastjson2', name: 'fastjson2', version: '2.0.49'
    jmhImplementation group: 'com.github.plokhotnyuk.jsoniter-scala', name: 'jsoniter-scala-core_2.13', version: jsoniterScalaVersion
    jmhImplementation group: 'com.google.guava', name: 'guava', version: '32.1.2-jre'
    compileOnly group: 'com.github.plokhotnyuk.jsoniter-scala', name: 'jsoniter-scala-macros_2.13', version: jsoniterScalaVersion
    compileOnly group: 'org.projectlombok', name: 'lombok', version: lombokVersion
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombokVersion
    testCompileOnly group: 'org.projectlombok', name: 'lombok', version: lombokVersion
    testAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombokVersion

    testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.24.2'
    testImplementation group: 'org.apache.commons', name: 'commons-text', version: '1.10.0'
    testImplementation group: 'org.junit-pioneer', name: 'junit-pioneer', version: '2.2.0'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: junitVersion
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: junitVersion
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: junitVersion

}

tasks.register('downloadTestData') {
    doFirst {
        def testDataDir = new File("${project.projectDir.getAbsolutePath()}/testdata")
        if (!testDataDir.exists()) {
            testDataDir.mkdir()
        }
        def numbersTestDataDir = new File("${testDataDir}/parse-number-fxx-test-data")
        if (!numbersTestDataDir.exists()) {
            def grgit = Grgit.clone(dir: numbersTestDataDir, uri: 'https://github.com/nigeltao/parse-number-fxx-test-data.git')
            grgit.close()
        }
    }
}

tasks.register('test256', Test) {
    dependsOn downloadTestData
    useJUnitPlatform()
    jvmArgs += [
            '--add-modules', 'jdk.incubator.vector',
            '-Xmx2g',
            '-Dorg.simdjson.species=256'
    ]
    testLogging {
        events 'PASSED', 'SKIPPED', 'FAILED', 'STANDARD_OUT', 'STANDARD_ERROR'
    }
}

tasks.register('test512', Test) {
    dependsOn downloadTestData
    useJUnitPlatform()
    jvmArgs += [
            '--add-modules', 'jdk.incubator.vector',
            '-Xmx2g',
            '-Dorg.simdjson.species=512'
    ]
    testLogging {
        events 'PASSED', 'SKIPPED', 'FAILED', 'STANDARD_OUT', 'STANDARD_ERROR'
    }
}

test {
    dependsOn 'test256'
    dependsOn 'test512'
}

tasks.withType(JmhBytecodeGeneratorTask).configureEach {
    jvmArgs.set(["--add-modules=jdk.incubator.vector"])
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs.add("--add-modules=jdk.incubator.vector")
}

tasks.compileJmhScala.classpath = sourceSets.main.compileClasspath

tasks.compileJmhJava.classpath += files(sourceSets.jmh.scala.classesDirectory)

compileTestJava {
    options.compilerArgs += [
            '--add-modules', 'jdk.incubator.vector'
    ]
}

javadoc.options {
    addStringOption('-add-modules', 'jdk.incubator.vector')
}

jmh {
    fork = 1
    warmupIterations = 3
    iterations = 5
    jvmArgsPrepend = [
            '--add-modules=jdk.incubator.vector'
    ]
    if (OperatingSystem.current().isLinux()) {
        def profilerList = []
        if (getBooleanProperty('jmh.asyncProfilerEnabled', false)) {
            createDirIfDoesNotExist('./profilers/async')
            profilerList += ['async:verbose=true;output=flamegraph;event=cpu;dir=./profilers/async;libPath=' + getLibPath('LD_LIBRARY_PATH')]
        }
        if (getBooleanProperty('jmh.perfAsmEnabled', false)) {
            createDirIfDoesNotExist('./profilers/perfasm')
            profilerList += ['perfasm:intelSyntax=true;saveLog=true;saveLogTo=./profilers/perfasm']
        }
        if (getBooleanProperty('jmh.perfEnabled', false)) {
            profilerList += ['perf']
        }
        profilers = profilerList
    }
    if (project.hasProperty('jmh.includes')) {
        includes = [project.findProperty('jmh.includes')]
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from(components.java)
            groupId = 'org.simdjson'
            artifactId = 'simdjson-java'
            version = scmVersion.version
            pom {
                name = project.name
                description = 'A Java version of simdjson, a high-performance JSON parser utilizing SIMD instructions.'
                url = 'https://github.com/simdjson/simdjson-java'
                issueManagement {
                    system = 'GitHub Issue Tracking'
                    url = 'https://github.com/simdjson/simdjson-java/issues'
                }
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'piotrrzysko'
                        name = 'Piotr RÅ¼ysko'
                        email = 'piotr.rzysko@gmail.com'
                    }
                }
                scm {
                    url = 'https://github.com/simdjson/simdjson-java'
                    connection = 'scm:git@github.com:simdjson/simdjson-java.git'
                    developerConnection = 'scm:git@github.com:simdjson/simdjson-java.git'
                }
            }
        }
    }
}

if (System.getenv('GPG_KEY_ID')) {
    signing {
        useInMemoryPgpKeys(
                System.getenv('GPG_KEY_ID'),
                System.getenv('GPG_PRIVATE_KEY'),
                System.getenv('GPG_PRIVATE_KEY_PASSWORD')
        )
        sign publishing.publications.mavenJava
    }
}

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl = uri('https://s01.oss.sonatype.org/service/local/')
            snapshotRepositoryUrl = uri('https://s01.oss.sonatype.org/content/repositories/snapshots/')
            stagingProfileId = '3c0bbfe420699e'
            username = System.getenv('SONATYPE_USERNAME')
            password = System.getenv('SONATYPE_PASSWORD')
        }
    }
    connectTimeout = Duration.ofMinutes(3)
    clientTimeout = Duration.ofMinutes(3)
}

def getBooleanProperty(String name, boolean defaultValue) {
    Boolean.valueOf((project.findProperty(name) ?: defaultValue) as String)
}

static def getLibPath(String envVarName) {
    System.getenv(envVarName) ?: System.getProperty('java.library.path')
}

static createDirIfDoesNotExist(String dir) {
    File file = new File(dir)
    file.mkdirs()
}
